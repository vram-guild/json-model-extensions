plugins {
	id 'fabric-loom' version '0.2.4-SNAPSHOT'
	//id 'java'
	//id 'maven'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

ext.getVersionCode = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--first-parent', '--count', 'origin/master'
            standardOutput = stdout
        }
        return Integer.parseInt(stdout.toString().trim())
    }
    catch (ignored) {
        return -1;
    }
}

archivesBaseName = "jmx"
group = "grondag"
version = version = "0.1.0+build." + project.getVersionCode();

minecraft {
}

repositories {
	mavenLocal()
    //flatDir {
    //    dirs '../fabric/build/libs'
    //}
    
    maven { url "https://tehnut.info/maven"}
    maven { url "http://maven.sargunv.s3-website-us-west-2.amazonaws.com/" }
    maven { url "https://minecraft.curseforge.com/api/maven" }
    maven { url "https://maven.fabricmc.net/io/github/prospector/modmenu/ModMenu/" }
    maven { url "https://maven.jamieswhiteshirt.com/libs-release/"}
    
 	maven {
    	name = "grondag"
    	url = "https://grondag-repo.appspot.com"
    	credentials {
            username "guest"
            password ""
		}
    }
}

processResources {
	inputs.property "version", version
	
	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": version
	}
	
	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}



// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

jar {
	from "LICENSE"
}

sourceSets {
	main {
	        java {
	            include 'grondag/**/*'
	            exclude '*.DS_Store'
	        }
	        resources
	        {
	        	include '**/*'
	        	exclude '*.DS_Store'
	        }
	    }
    test {
        java {
            exclude 'grondag/**'
        }
    }
}

def mcVersion = "1.14.2"

dependencies {
	minecraft "com.mojang:minecraft:$mcVersion"
	mappings "net.fabricmc:yarn:$mcVersion+build.7"
    modImplementation "net.fabricmc:fabric-loader:0.4.8+build.154"
    modImplementation "net.fabricmc.fabric-api:fabric-api:0.3.0+build.180"
	
	modImplementation "grondag:canvas:0.7.0+pre.314"

	compile "com.google.code.findbugs:jsr305:3.0.2"
		
    modImplementation "grondag:fermion:0.6.+"
    include "grondag:fermion:0.6.+"
	
	modCompile "grondag:frex:0.7.0+build.52"
    include "grondag:frex:0.7.0+build.52"
    
	// For dev env testing 
	modImplementation "com.jamieswhiteshirt:developer-mode:1.0.11"
	modImplementation "io.github.prospector.modmenu:ModMenu:1.5.4-85"
	
	//modCompile "cloth:ClothEvents:0.3.1.23" // for REI
	//modCompile "roughly-enough-items:RoughlyEnoughItems:2.7.11.97"
	
	modImplementation "grondag:instruments:0.0.1+build.3"
	
	// dev env annotation support
	compile "org.apiguardian:apiguardian-api:1.0.0"
	compile "com.google.code.findbugs:jsr305:3.0.2"
	
}

publishing {
    publications {
        mavenJava(MavenPublication) {
			artifact(jar.archivePath) {
	    		builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
        }
    }

    repositories {
        maven {
            url "https://grondag-repo.appspot.com"
            credentials {
            	if (project.hasProperty('maven_user')){
					username = project.getProperty('maven_user')
				}
				if (project.hasProperty('maven_password')){
					password = project.getProperty('maven_password')
				}
			}
        }
    }
}